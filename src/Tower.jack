class Tower{
    field Array blocksInTower;
    field int thresholdCount;
    field int index;
    

    constructor Tower new(int _thresholdCount){
        let thresholdCount = _thresholdCount;
        let blocksInTower = Array.new(thresholdCount);
        let index = 0;
        return this;
    }

    method void putBlock(Block block){
        var int prevLeftX, prevRightX;
        var int currentLeftX, currentRightX;
        var int currentLeftY;
        var Block previousBlock, lastBlock;

        if (index = 0){
            let blocksInTower[index] = block;
            let index = index + 1;
            return;
        }

        let previousBlock = blocksInTower[index - 1];
        let prevLeftX = previousBlock.getX();
        let prevRightX = prevLeftX + previousBlock.getWidth() - 1;

        let currentLeftX = block.getX();
        let currentRightX = currentLeftX + block.getWidth() - 1;
        let currentLeftY = block.getY();

        do reduceLastBlockWidth(block, prevLeftX, prevRightX, currentLeftX, currentRightX, currentLeftY);
        do block.erase();
        do block.dispose();

        let lastBlock = blocksInTower[index];
        do lastBlock.draw(); 

        if (index < (thresholdCount - 1)){
            let index = index + 1;
        } else {
            do moveDown();
        }
        return;
    }

    method void reduceLastBlockWidth(Block block, int prevLeftX, int prevRightX, int currentLeftX, int currentRightX, int currentLeftY){
        var int newBlockWidth;
        if ((currentRightX < prevLeftX) | (currentLeftX > prevRightX)){
            let newBlockWidth = 0;
        }

        if (((currentLeftX < prevLeftX) | (currentLeftX = prevLeftX)) 
            & ((prevLeftX < currentRightX) | (prevLeftX = currentRightX))){
            let newBlockWidth = currentRightX - prevLeftX + 1;
            let blocksInTower[index] = Block.new(prevLeftX, currentLeftY, 0, newBlockWidth, block.getHeight());
        }

        if (((currentLeftX < prevRightX) | (currentLeftX = prevRightX)) 
            & ((prevRightX < currentRightX) | (prevRightX = currentRightX))){
            let newBlockWidth = prevRightX - currentLeftX + 1;
            let blocksInTower[index] = Block.new(currentLeftX, currentLeftY, 0, newBlockWidth, block.getHeight()); 
        }
        do TowerBuilder.updateWidth(newBlockWidth);
        return;
    }

    method void moveDown(){
        var int j, cnt, blockHeight;
        var Block block;
        let block = blocksInTower[0];
        let blockHeight = block.getHeight();

        if (TowerBuilder.getBlockWidth() = 0){
            return;
        }

        while(cnt < blockHeight){
            let j = 0;
            while(j < thresholdCount){
                let block = blocksInTower[j];
                do block.moveY();
                let j = j + 1;
            }
            do Sys.wait(3);
            let cnt = cnt + 1;        
        }

        let j = 0;
        let block = blocksInTower[0];
        do block.dispose();
        while(j < (thresholdCount - 1)){
            let blocksInTower[j] = blocksInTower[j + 1];
            let j = j + 1;
        }
        return;
    }

    method int getThresholdCount(){
        return thresholdCount;
    }

    method void dispose(){
        do Memory.deAlloc(blocksInTower);
        do Memory.deAlloc(this);
        return;
    }
}
