class TowerBuilder {
    static int blockWidth;
    static int blockHeight;
    static Tower tower;
    static int score;

    function void run() {
        var Block block;
        var int currentY;
        let blockWidth = 250;
        let blockHeight = 25;
        let tower = Tower.new(128 / blockHeight + 1);
        let currentY = 256 - blockHeight;
        
        do TowerBuilder.showHint();
        do TowerBuilder.printScore();
        while(true) {
            if(block = null){
                let block = TowerBuilder.createNewBlock(currentY);
                do block.draw();
            }

            do block.moveX();
            if (Keyboard.keyPressed() = 32){
                do tower.putBlock(block);
                let score = score + 1;
                do TowerBuilder.printScore();
                if (score = 2){
                    do TowerBuilder.hideHint();
                }
                if(score < tower.getThresholdCount()){
                    let currentY = currentY - blockHeight;
                }
                let block = null;
                do Sys.wait(200);
            }
            do Sys.wait(2);
        }
        return;
    }

    function Block createNewBlock(int currentY){
        if ((score / 2 * 2) = score){
            return Block.new(0, currentY, 1, blockWidth, blockHeight);
        }
        return Block.new(512 - blockWidth, currentY, -1, blockWidth, blockHeight);
    }

    function void updateWidth(int newBlockWidth){
        let blockWidth = newBlockWidth;
        return;
    }

    function void showHint(){
        var String hint;
        let hint = "Press space to place a block";
        do Output.moveCursor(10, 18);
        do Output.printString(hint);
        do Memory.deAlloc(hint);
        return;
    }

    function void hideHint(){
        var String spacesLine;
        let spacesLine = "                             ";
        do Output.moveCursor(10, 17);
        do Output.printString(spacesLine);
        do Memory.deAlloc(spacesLine);
        return;
    }

    function void showGameOver(){
        do Screen.clearScreen();
        do Output.moveCursor(10, 26);
        do Output.printString("Game Over!");
        do Output.moveCursor(11, 24);
        do Output.printString("Your score: ");
        do Output.printInt(score);
        do Sys.halt();
        return;
    }

    function void printScore(){
        do Output.moveCursor(0, 0);
        do Output.printString("Score: ");
        do Output.printInt(score);
        return;
    }
}
